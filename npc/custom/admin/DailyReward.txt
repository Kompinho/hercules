//===== EinherjarRO Scripts ================================== 
//= Daily Prize, OnPCLoginEvent
//===== By: ================================================== 
//= Stolao
//===== Current Version: ===================================== 
//= 1.42
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= A reward system for players who play more frequently
//===== Comments: ============================================
//= N/A
//===== Additional Comments: =================================
//= 1.00 Daily Prize
//= 1.01 Fixed Typo
//= 1.02 Removed unessisary calculation
//= 1.03 Added a sleep2 for delayed reward
//= 1.04 Added dispbottom
//= 1.05 Made Clearer
//= 1.06 Made Disbutton Dynamic
//= 1.07 Added .RewardQnt for configable Reward Quantity
//= 1.08 Fixed Typo
//= 1.09 Seperated .ZMulti into .Mode and .ZMulti
//= 1.0A Changed zeny formula for .Mode 0
//= 1.0B Changed Variable Names, More detailed exsplanations
//= 1.0C Fixed typo again lol
//= 1.0D Added in .PointType$, can now configure zeny into any point types
//= 1.0E Added LoginCount
//= 1.0F Made LoginCount Customisable
//= 1.10 Changed Variable Names, More detailed exsplanations
//= 1.11 Added Wipe feature
//= 1.12 Fixed bug, changed "close;" to "close2;"
//= 1.13 Changed .LogCount to bitwise
//= 1.14 Changed .wipe to a whisper event, per Euphys Suggestion
//= 1.15 Changed Formatting of config, per Euphys Suggestion
//= 1.16 Added Login Reward Countdown, on each login
//= 1.17 Added .PointName$
//= 1.18 Fixed Typo
//= 1.19 Added Checkweight
//= 1.1A Changed query_sql for $LOGINCOUNT to set
//= 1.1B Fixed bug with #DRewardCon, arraysize compersison (Thanks AnnieRuru)
//= 1.1C Added meathod for characters online for attachrid
//= 1.1D Actually Fixed the 1.1B Bug
//= 1.1F Changed zeny formula to remove .ZMulti from mode 0
//= 1.20 Changed .Reminder to .Welcome
//= 1.21 Fixed missing details in Welcome Message
//= 1.22 Changed .Mode to be bitwise
//= 1.23 Added Item enable disable to .Mode
//= 1.24 Changed whisper to allow for player Whispers
//= 1.25 Added "time" Whisper Command for players, Thanks arzzae for suggestion
//= 1.26 Made Whisper commands configable
//= 1.27 Made GmLvl for wipe Configable
//= 1.28 Fixed "Points" bitwise
//= 1.29 Added Exp to .Mode
//= 1.2A Optimized Rewards Script a little
//= 1.2B Bug Fix with Display message on reciving rewards
//= 1.2C Optimize a little more
//= 1.2D Added server xp rate to config
//= 1.2F Re-added checkweight
//= 1.30 Move Checkweight up higher in script, makes more since
//= 1.31 Removed XP Dispbutton
//= 1.32 Added Buffer
//= 1.33 Made BUffer Toggle-able
//= 1.34 Made Buffer Dynamic
//= 1.35 Optimized Buffer a little
//= 1.36 Made Buffer More Dynamic
//= 1.37 Optimized Buffer a little more
//= 1.38 Fixed a typo in "seem" was "see"
//= 1.39 removed "typo" bindatcmds
//= 1.3A Fixed bug with sc_, didn't support names had to switch to ids
//= 1.3B Fixed bug in buff bonus (time and rate)
//= 1.3C Revamped buffs to take only 1 variable (now can only be 32 buffs)
//= 1.3D Made Day(s) dynamic
//= 1.3E Removed "Time" reqirement from players to check there Next Login Bonus
//= 1.3F Added an Else after checkweight
//= 1.40 Fixed typo in .BuffInfo
//= 1.41 Reorganized rewards script
//= 1.42 changed .@i -> .@x in Option & 32 (bug report by rakuzas)
//===== Contact Ifo: =========================================
//= [Stolao] 
//= Email: Taingram11@gmail.com
//============================================================
-	script	LOGIN	-1,{
OnWhisperGlobal:
	if (@whispervar0$ == .Wipe$){
		if (getgmlevel() < .GmLvl) end;
		query_sql("DELETE FROM `global_reg_value` WHERE `str` = 'LOGINCOUNT'");
		query_sql("DELETE FROM `global_reg_value` WHERE `str` = '#LOGINCOUNT'");
		query_sql("DELETE FROM `global_reg_value` WHERE `str` = '#LastDailyReward'");
		set $LOGINCOUNT,0;
		set .@self, getcharid(3);
		set .@size, query_sql("select account_id from `char` where online = 1", .@aid);
		for(set .@i,0; .@i<.@size; set .@i,.@i+1) {
			if(attachrid(.@aid[.@i])){
				set LOGINCOUNT,0;
				set #LOGINCOUNT,0;
				set #LastDailyReward,0;
			}	
		}
	attachrid(.@self);
	dispbottom "Cleared all login count data.";
	}
OnLoginCmnd:
	set .@i,gettimetick(2) + 86400; //24 hours.
	set .@last,#LastDailyReward - gettimetick(2);
	set .@hour, .@last % ( 24 * 3600 ) / 3600;
	set .@min,  .@last % ( 24 * 3600 ) % 3600 / 60;
	set .@sec,  .@last % ( 24 * 3600 ) % 3600 % 60;
	if(#LastDailyReward < gettimetick(2)) {
		dispbottom "Your login reward is available, please relog to calim your prize~";
	} 
	else { 
		dispbottom "Next VIP awards will be in : "+.@hour+":"+.@min+":"+.@sec+".";
	}
	end;
OnPCLoginEvent:
	if(.LogCount){
		if(.LogCount & 1) set LOGINCOUNT,LOGINCOUNT+1;
		if(.LogCount & 2) set #LOGINCOUNT,#LOGINCOUNT+1;
		if(.LogCount & 4) set $LOGINCOUNT,$LOGINCOUNT+1;
		if( ( LOGINCOUNT + #LOGINCOUNT + $LOGINCOUNT )%.Welcome[1] == 0 || (LOGINCOUNT + #LOGINCOUNT) < .Welcome[0] ){
			Mes "~Welcome to "+.Servername$+"~";
			Mes .Message$;
			close2;
		}
	}
	set .@getAccountType$,query_sql("SELECT `account_type` FROM login WHERE `account_id` = "+getcharid(3)+";",.@accountType$);
	if(compare (.@accountType$,"NORMAL")) {
		set .@accountType,0;
	}
	if(compare (.@accountType$,"PLATINUM")) {
		set .@accountType,3;
	}
	if(compare (.@accountType$,"GOLD")) {
		set .@accountType,2;
	}
	if(compare (.@accountType$,"SILVER")) {
		set .@accountType,1;
	}
	set .@last,#LastDailyReward - gettimetick(2);
	set .@hour, .@last % ( 24 * 3600 ) / 3600;
	set .@min,  .@last % ( 24 * 3600 ) % 3600 / 60;
	set .@sec,  .@last % ( 24 * 3600 ) % 3600 % 60;
	if(.DailyPrize){
		if( #DRewardCon == 0 ) set #DRewardCon,1; 
		set .@i,gettimetick(2) + .MinWait; //24 hours.
		set .@g,getarraysize(.Rewards);
		sleep2 .Rest*60000;
		if(#LastDailyReward < gettimetick(2)) {
			if(.@i < #LastDailyReward + .MaxWait){ set #DRewardCon,#DRewardCon+1;
			 } else { set #DRewardCon,1; }
			if(#DRewardCon*2 > .@g-1){ set .@p,.Rewards[.@g-2]; set .@q,.Rewards[.@g-1];
			} else { set .@p,.Rewards[#DRewardCon*2-2];  set .@q,.Rewards[#DRewardCon*2-1]*.VIPRewards[.@accountType]; }
			if(.Mode&1&&.@p){ 
				if(!checkweight(.@p,.@q)){ dispbottom "You seem to be oveweight, put some stuff away and relog to claim prize"; end;
				} else { getitem .@p,.@q; dispbottom ""+.@q+" "+getitemname(.@p)+""; }
			}
			if(.Mode&2) set .@A,#DRewardCon*.ZMulti;
			if(.Mode&8) set .@A,.@A+.@q;
			if(.Mode&2||.Mode&8){  setd .PointType$,getd(.PointType$)+.@A; dispbottom ""+.@A+" "+.PointType$+""; }
			if(.Mode&4){ set .@B,#DRewardCon*.XPMulti[0]; set .@C,#DRewardCon*.XPMulti[1]; }
			if(.Mode&16){ set .@B,.@B+.@q; set .@C,.@C+.@q; }
			if(.Mode&4||.Mode&16){ getexp .@B,.@C; }
			if(.Mode&32){
				for(set .@x,0; .@x < getarraysize(.BuffInfo); set .@x,.@x+4){
					if(#DRewardCon % .BuffInfo[.@x+1] == 0) sc_start .BuffInfo[.@x], .BuffInfo[.@x+2]*60000, .BuffInfo[.@x+3];
				}
			}
			dispbottom "You have collected your daily reward as a "+.@accountType$+" account, for "+#DRewardCon+" day"+((DRewardCon>1)?"s":"")+" in a row.";
			set #LastDailyReward,.@i;
		} else { 
			dispbottom "Next VIP awards will be in : "+.@hour+":"+.@min+":"+.@sec+".";
			
		}
	}
end;
OnInit:
// -----------------------------------------------------------
//  Login Count & Welcome Message
// -----------------------------------------------------------

	// Login types to track.
	//   1: Character | 2: Account | 4: Server
	// (a bit value, e.g. 5 = character + server)
	set .LogCount,2;

	// Number of logins to display welcome message.
	//  [0]: First x Logins, 0 will never give a message
	//  [1]: Remider message every x Logins (combined server account and character)
	setarray .Welcome,3,250;

	// Server name to display.
	set .Servername$,"ExhilarateRO";

	// Login message
	// * Must have at least 1 Login Count Enabled
	set .Message$,"Before playing, make sure you've read the rules, located in our forum at ^0000FFboard.exhilarate-ro.com^000000";

// -----------------------------------------------------------
//  Daily Reward
// -----------------------------------------------------------

	//To Enable @ Command '@LoginReward' unslash next lines
	// * Needs extra commands for typos
	bindatcmd("loginreward"	,"LOGIN::OnLoginCmnd",0,99);

	//Data Wipe Whisper Command
	set .Wipe$,"wipe";

	//GM Level to use Wipe Whisper Command
	//   Keep at 99 for security
	set .GmLvl,99;

	//Daily Prize
	//Toggle
	//   0: Off  | 1: On
	set .DailyPrize,1;

	//Minimum Hours Between Collecting Daily Reward
	// 24 hours = 24 x 3600
	set .MinWait,86400;

	//Hours Before Lose Consecutive Daily Reward
	// 50 Days = 24 x 50 x 3600
	set .MaxWait,216000000;

	//Number of mins after logging before collecting prize
	set .Rest,0;

	//Type of Points/Zeny earned 
	//   eg: CASHPOINTS, Zeny, LoginPoints
	set .PointType$,"Zeny";

	//Toggle 
	//   1: Item | 2: "Points" from Multi | 4: Exp from Multi 
	//   8: "Points" from Days with ItemID of 0 |16: Exp from Days with ItemID of 0
	//   32: Gain Buffs Every X Consecutive Days logged in
	// (a bit value, e.g. 3 = Items & Points from Multi)
	set .Mode,1+2+4+8+16+32;

	//Consecutive Day Points Multiplier
	// * If players login longer than the last set
	//   day, they will keep getting a larger Multiplier
	set .ZMulti,1000;

	//Consecutive Day Exp Multiplier
	//  [0]: Base
	//  [1]: Job
	// * If players login longer than the last set
	//   day, they will keep getting a larger Multiplier.
	setarray .XPMulti,50,50;

	// Consecutive Days Buff
	// Each buff contains 4 variables (32 Total Max)
	// Type,Days,Time,Rate
	//  *Example: 188,7,45,3
	//  *Every 7th consecutive day logged in Player gains +3 Str for 45 mins
	//  Type is 188, which references which SC_ to use, SC_INCSTR in this case
	//     -For a full list of SC_ visit the db/const.txt
	//  Days is days buff is applied, in this case 7, so Days 7,14,21,28....
	//  Time is buff duration is Minuits, in this case 45 mins
	//  Rate is buff strength, in this case player gains 3 Str
	setarray .BuffInfo	,188,7,45,3	// +3 Str for 45 Mins Every 7th Day
				,189,7,45,3	// +3 Agi for 45 Mins Every 7th Day
				,190,7,45,3	// +3 Vit for 45 Mins Every 7th Day
				,191,7,45,3	// +3 Int for 45 Mins Every 7th Day
				,192,7,45,3	// +3 Dex for 45 Mins Every 7th Day
				,193,7,45,3	// +3 Luk for 45 Mins Every 7th Day
				,194,4,45,25	// +25 Hit for 45 Mins Every 4th Day
				,196,4,45,25	// +25 Flee for 45 Mins Every 4th Day
				,198,10,60,10	// +10% Hp for 60 Mins Every 10th Day
				,199,10,60,10	// +10% Sp for 60 Mins Every 10th Day
				,200,13,30,10	// +10% Atk for 30 Mins Every 10th Day
				,201,13,30,10	// +10% Matk for 30 Mins Every 10th Day
				,257,9,120,50	// +50% Exp for 120 Mins Every 9th Day
				,258,9,120,50;	// +50% Item Drops for 120 Mins Every 9th Day

	// Daily Prize items (max 64 days):
	//   <itemID>,<amount>, // Day 1
	//   <itemID>,<amount>, // Day 2
	//   ...;
	// * If players login longer than the last set
	//   day, they will keep getting the last prize.
	setarray .Rewards[0],
		512,1,		// Day 1
		513,2,		// Day 2
		514,3;		// Day 3

	setarray .VIPRewards[0],
		1,		// NORMAL
		2,		// SILVER
		3,		// GOLD
		4;		// PLATINUM
end;
}